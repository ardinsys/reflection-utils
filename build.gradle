apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'findbugs'
apply plugin: 'jacoco'

group 'eu.ardinsys'
version '1.0.0'
archivesBaseName = 'reflection-utils'

sourceCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.ow2.asm', name: 'asm', version: '6.1.1'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(
                        project.hasProperty('sonatypeUsername') ? project.sonatypeUsername : System.getenv('sonatypeUsername'),
                        project.hasProperty('sonatypePassword') ? project.sonatypePassword : System.getenv('sonatypePassword')
                )
            }

            pom.project {
                name 'reflection-utils'
                packaging 'jar'
                description 'Ardinsys Reflection utils'
                url 'https://github.com/ardinsys/reflection-utils'

                scm {
                    url 'scm:https://github.com/ardinsys/reflection-utils.git'
                    connection 'scm:https://github.com/ardinsys/reflection-utils.git'
                    developerConnection 'scm:git@github.com:ardinsys/reflection-utils.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'vm-ardinsys'
                        name 'Viktor Magyari'
                        email 'magyari.viktor@ardinsys.eu'
                    }
                }
            }
        }
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

findbugs {
    ignoreFailures = true
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}